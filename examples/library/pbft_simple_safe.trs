protocol PBFTSimpleSafe {
    params n, t, f;
    resilience: n > 3*t;

    adversary {
        model: byzantine;
        bound: f;
    }

    message PrePrepare;
    message Prepare;
    message Commit;

    role Replica {
        var decided: bool = false;
        var decision: bool = false;

        init start;

        phase start {
            when received >= 1 PrePrepare => {
                send Prepare;
                goto phase prepared;
            }
        }

        phase prepared {
            when received >= 2*t+1 Prepare => {
                send Commit;
                goto phase committed;
            }
        }

        phase committed {
            when received >= 2*t+1 Commit => {
                decision = true;
                decided = true;
                decide true;
                goto phase done;
            }
        }

        phase done {}
    }

    property agreement: agreement {
        forall p: Replica. forall q: Replica.
            (p.decided == true && q.decided == true) ==> (p.decision == q.decision)
    }
}
